#!/usr/bin/perl

use strict;
use warnings;

my $root = $ENV{BBOP_ROOT} // '/var/local/backup';

while (<STDIN>) {
    chomp;
    my ($target, $series, $backup, $tape, $tapefile, $filename, $offset, $length, $meta) = split / /, $_, 9;
    my $stored = "$root/targets/$target/series/$series/$filename";
    if (! -e $stored) {
        print STDERR "First read tape file $tapefile from tape $tape\n";
        exit 2;
    }
    print STDERR "\e[32;1m", $filename, "\e[0m\n";
    open my $fh, '-|', 'gunzip', '-c', $stored or die "bbget: open $stored: $!";
    my $buf;
    while ($offset > 0) {
        my $n = $offset < 32768 ? $offset : 32768;
        $offset -= (sysread($fh, $buf, $n) || die "bbget: read $stored: $!");
    }
    while ($length > 0) {
        my $n = $length < 32768 ? $length : 32768;
        $length -= (sysread($fh, $buf, $n) || die "bbget: read $stored: $!");
        print STDOUT $buf;
    }
}
