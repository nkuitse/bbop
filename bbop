#!/bin/zsh -e

typeset PROG=$0
typeset NAME=${PROG:t}
typeset -a targets
typeset dryrun=false verbose=false

[[ -n $BB_ROOT ]] || export BB_ROOT=/var/local/backup

main() {
    [[ -x $BB_ROOT ]] || fatal "root $BB_ROOT not accessible"
    cd $BB_ROOT || return
    [[ $NAME == bb* ]] || usage
    typeset cmd=${NAME[3,-1]}
    case $cmd in
        (op) backup "$@" ;;
        (*)  cmd_$cmd "$@" ;;
    esac
}

backup() {
    typeset opt yes=false all=false
    typeset -a zubakopts
    while getopts :nvyal opt; do
        case $opt in
            (n) dryrun=true ;;
            (v) verbose=true ;;
            (y) yes=true ;;
            (a) all=true ;;
            (l) zubakopts+=( -l ) ;;
            (*) usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    if (( $# == 0 )); then
        $all || usage
        if ! $yes && [[ -t 0 ]]; then
            askbool 'Back up all targets?' || cancel
            yes=true
        fi
        set -- $(zubak lt)
    fi
    targets=( $(targets $@) )
    (( $#targets > 0 )) || fatal 'nothing to back up'
    if ! $yes && [[ -t 0 ]]; then
        print 'Targets selected for backup:' >&2
        for t in ${(u)targets}; do print "  * $t"; done >&2
        askbool 'OK to proceed?' || cancel
    fi
    for t in ${(u)targets}; do
        run zubak up $zubakopts $t
    done |
    while read tdone s mode b; do
        make-t0p-job $tdone $s $mode $b | read job
        t0queue -a $job
        # TODO: print $tdone $s $mode $b $job
    done
    $dryrun || print 'Write jobs to tape' >&2
    run t0write -a
    print 'Done' >&2
}

cmd_start() {
    typeset opt yes=false all=false
    typeset -a bbargs
    while getopts :vyal opt; do
        case $opt in
            (v) bbargs+=( -v ) ;;
            (y) yes=true ;;
            (a) all=true; bbargs+=( -a ) ;;
            (l) bbargs+=( -l ) ;;
            (*) usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    if (( $# == 0 )); then
        $all || usage
        if ! $yes && [[ -t 0 ]]; then
            askbool 'Back up all targets?' || cancel
        fi
        set -- $(zubak lt)
    fi
    targets=( $(targets $@) )
    $all || (( $#targets > 0 )) || fatal 'nothing to back up'
    $PROG -y $bbargs $targets > bbop.$$.log 2>&1 &
}

cmd_new() {
    typeset opt host root
    (( $# == 1 )) || usage TARGET
    typeset t=$1; shift
    case $t in
        (*/*)
            host=$t:h
            root=/$t:t
            [[ $host$root == $t ]] || usage TARGET
            ;;
        (*) usage ;;
    esac
    typeset host=$( { print $HOST:r; hostname -s } | sort -u | fgrep -v . )
    [[ ! -e targets/$t ]] || fatal "target already exists: $t"
    zubak nt $t
    ### [[ -e targets/$t ]] || fatal "target not created: $t"
    ### [[ -e targets/$t/bin/zubak-begin ]] || make-zubak-begin $t $host
    ### [[ -e targets/$t/bin/zubak-end   ]] || make-zubak-end   $t $host
    ### make-zubak-find $t $host $root
}

cmd_conf() {
    targets=( $(targets "$@") )
    (( $#targets == 1 )) || fatal 'no targets found'
}

cmd_queue() {
    typeset usage='[-v] [-dwm] [-akhutl] ...'
    typeset opt q qheld list=true add=false clear=false release=false hold=false tally=false all=false future=false running=false dly wly mly
    typeset -a targets queues
    print -P '%D{d%H w%a m%d}' | tr A-Z a-z | read dly wly mly
    while getopts :vakhutlAFRdwm opt; do
        case $opt in
            (v) verbose=true ;;
            (a) add=true ;;
            (k) clear=true ;;
            (h) hold=true ;;
            (u) release=true ;;
            (t) tally=true ;;
            (l) list=true ;;
                (A) all=true ;;
                (F) future=true; running=false ;;
                (R) running=true; future=false ;;
            (d) queues+=( $dly ) ;;
            (w) queues+=( $wly ) ;;
            (m) queues+=( $mly ) ;;
            (*) usage $usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    if $add; then
        usage='-a [-v] [-dwm] [-khutl] QUEUE TARGET...'
        (( $#queues < 2 )) || usage $usage
        (( $#queues == 0 )) || set $queues $@
        (( $# > 1 )) || usage $usage
        q=$1; shift
        if [[ $q != [0-9] && $q != m[0-9][0-9] && $q != w[a-z][a-z][a-z] && $q != d[0-9][0-9] ]]; then
            print "bbqueue: warning: adding to a non-standard queue: $q" >&2
        fi
        ! $hold || q=${q}+
        print -l $(targets "$@") >> queue/$q
    elif $clear; then
        usage='-k [-h] [-dwm|QUEUE]...'
        set -- $queues $@
        (( $# )) || usage $usage
        for q in $@:t; do
            ! $hold || q=${q}+
            [[ ! -s queue/$q ]] || : > queue/$q
        done
    elif $hold; then
        usage='-h [-dwm|QUEUE]...'
        set -- $queues $@
        (( $# )) || set -- queue/$dly(.N) queue/$wly(.N) queue/$mly(.N)
        (( $# )) || return 0
        for q in $@:t; do
            ! isempty queue/$q || continue 
            qheld=${q}+
            if [[ -e queue/$qheld ]]; then
                cat queue/$q >> queue/$qheld
                : > rm queue/$q
            else
                mv queue/$q queue/$qheld
            fi
        done
    elif $release; then
        usage='-u [-dwm|QUEUE]...'
        set -- $queues $@
        (( $# )) || set -- queue/$dly+(.N) queue/$wly+(.N) queue/$mly+(.N)
        (( $# )) || return 0
        for q in $@:t; do
            if [[ $q == *+ ]]; then
                qheld=$q
                q=${q[1,-2]}
            else
                qheld=${q}+
            fi
            if [[ -e queue/$q ]]; then
                if [[ -e queue/$qheld ]]; then
                    cat queue/$qheld >> queue/$q
                    rm queue/$qheld
                fi
            elif [[ -e queue/$qheld ]]; then
                mv queue/$qheld queue/$q
            fi
        done
    elif $tally; then
        set -- $queues $@
        (( $# )) || set -- queue/[0-9](.N) queue/$dly(.N) queue/$wly(.N) queue/$mly(.N)
        if $verbose; then
            for q in $@:t; do
                if [[ -e queue/$q ]]; then
                    wc -l queue/$q
                else
                    print 0 $q
                fi | sed s:queue/::
            done
        else
            for q in $@:t; do
                [[ ! -e queue/$q ]] || cat queue/$q
            done | sort -u | wc -l
        fi
    else
        if $all; then
            verbose=true
            set -- queue/*(.N)
        elif $future; then
            verbose=true
            set -- queue/*+(.N)
        elif $running; then
            verbose=true
            set -- queue/*@(.N)
        else
            set -- $queues $@
            (( $# )) || set -- queue/[0-9](.N) queue/$dly(.N) queue/$wly(.N) queue/$mly(.N)
        fi
        (( $# )) || exit 0
        if $verbose; then
            for q in $@:t; do
                [[ ! -e queue/$q ]] || sort -u queue/$q | sed "s/^/$q /"
            done
        else
            for q in $@:t; do
                [[ ! -s queue/$q ]] || targets+=( $(cat queue/$q) )
            done
            targets=( ${(u)targets} )
            (( $#targets == 0 )) || print -l ${(u)targets}
        fi
    fi
}

cmd_run() {
    typeset opt q dly wly mly
    typeset -a queues targets backupargs
    queues=( queue/*@(.N) )
    (( $#queues == 0 )) || fatal "the queue is running: $queues"
    print -P '%D{d%H w%a m%d}' | tr A-Z a-z | read dly wly mly
    while getopts :nyvadwm opt; do
        case $opt in
            (n) dryrun=true; backupargs+=( -n ) ;;
            (y) backupargs+=( -y ) ;;
            (v) verbose=true; backupargs+=( -v ) ;;
            (a) queues=( queue/[0-9](.N) queue/$dly(.N) queue/$wly(.N) queue/$mly(.N) )
                (( $#queues )) || exit 0
                ;;
            (d) queues+=( queue/$dly(.N) ) ;;
            (w) queues+=( queue/$wly(.N) ) ;;
            (m) queues+=( queue/$mly(.N) ) ;;
            (*) usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    if (( $#queues == 0 )); then
        (( $# )) || usage
        for q in $@:t; do
            [[ -e queue/$q ]] || fatal "no such queue: $q"
            queues+=( queue/$q )
        done
    fi
    targets=( $(cat $queues) )
    (( $#targets )) || return 0
    for q in $queues:t; do
        $dryrun || mv queue/$q queue/$q@
    done
    run backup $backupargs ${(u)targets}
    for q in $queues:t; do
        if $dryrun; then
            :
        elif [[ $q == [0-9] ]]; then
            rm queue/$q@
            : > queue/$q
        elif [[ -e queue/$q+ ]]; then
            rm queue/$q@
        else
            mv queue/$q@ queue/$q+
        fi
    done
}

cmd_inv() {
    typeset opt want wantmode tlastfull mode tape size total
    typeset -a tbackups backups
    typeset -A tsize
    while getopts :nvpkfi opt; do
        case $opt in
            (n) dryrun=true ;;
            (v) verbose=true ;;
            (p) want=P ;;
            (k) want=K ;;
            (f) wantmode=full ;;
            (i) wantmode=incr ;;
            (*) usage ;;
        esac
    done
    shift $(( OPTIND - 1 ))
    targets=( $(targets $@) )
    for t in ${(u)targets}; do
        tbackups=()
        tlastfull=':'
        for s in $(zubak ls $t); do
            set -- $(zubak lb $t $s)
            (( # )) || continue
            tlastfull=${s}:${1}
            tbackups+=( $s $1 full )
            shift
            for b in $@; do
                tbackups+=( $s $b incr )
            done
        done
        typeset action=P
        for s b mode in $tbackups; do
            [[ -z $wantmode || $mode == $wantmode ]] || continue
            set -- targets/$t/series/$s/$b.[0-9]*(N)
            if [[ ${s}:${b} == $tlastfull ]]; then
                action=K
                [[ -z $want || $want == K ]] || continue
            elif (( ! $# )); then
                continue
            fi
            du -bc $@ | tail -n1 | read size total
            if backup-tape $t $s $b | read tape; then
                (( tsize[$t] += size ))
            else
                tape=-
            fi
            backups+=( $action $size $t $s $b $mode $tape )
        done
    done
    for action size t s b mode tape in $backups; do
        [[ -n $want && $want != $action ]] || print $action $size $t $s $b $mode $tape
    done
}

# --- Functions

isempty() {
    [[ ! -s $1 ]]
}

note() {
    if [[ -t 1 ]]; then
        print -- "\e[35;1m$*\e[0m"
    else
        print -- "$*"
    fi
}

run() {
    if $verbose; then
        note "$*"
        $dryrun && print '  (dry run)'
    fi
    $dryrun || "$@"
}

askbool() {
    read -q "?$1 [yN] "
    integer err=$?
    [[ $REPLY == $'\n' ]] || print
    return $err
}

find-on-tape() {
    while read t s b m; do
        f=$(perl -e '$m=shift; $m =~ m{[^/]+(.+)}; print $1, "\n"')
        if [[ ! -e $s/$b.t0receipt ]]; then
            print "$t $s $b <> $m"
            continue
        fi
        #print perl -ne ... $t $s $b "'$m'" \< $PWD/$s/$b.t0receipt >&2
        perl -e '
            BEGIN { ($tape, $n, $tgt, $s, $bkup, $m) = (qw(- -), @ARGV) }
            die if $m !~ /^\^(\d+)/;
            $b = $1;
            die if $m !~ m{[^/]+(/.*)};
            $f = $1;
            while (<STDIN>) {
                if (/^J (\S+)/) {
                    $j = $1;
                }
                elsif (/^T (\S+) filenum (\d+)/) {
                    ($tape, $n) = ($1, $2);
                }
                elsif (/^F (\d+) (\S+)\.(\d+)\.gz/) {
                    if ($3 == $b) {
                        print "$tgt $s $bkup <$j:$b:$tape:$n> $m\n";
                    }
                }
            }
        ' $t $s $b $m < $s/$b.t0receipt
    done
}

strip-final-slashes() {
    typeset f=$1
    while (( $#f > 1 )) && [[ $f == */ ]]; do
        # Strip final /, //, etc.
        f="${f[1,-2]}"
    done
    print -- "$f"
}

parse-date() {
    print ${1[1,4]} ${1[5,6]} ${1[7,8]} ${1[10,11]} ${1[12,13]} ${1[14,15]}
}

targets() {
    (( $# > 0 )) || targets=( $(zubak lt) )
    for t in $@; do
        if [[ $t == */* ]]; then
            targets+=$t
        elif [[ -e targets/$t/series ]]; then
            targets+=$t
        else
            targets+=( $(zubak lt | grep "^$t/") )
        fi
    done
    (( $#targets > 0 )) || fatal "no such target: $t"
    print -l $targets
}

backup-size() {
    typeset t=$1 s=$2 b=$3
    du -hc targets/$t/series/$s/$b.* | awk '{t=$1} END {print t}'
}

backup-duration() {
    sec2approxdur $(backup-duration-raw $@)
}

backup-duration-raw() {
    typeset t=$1 s=$2 b=$3
    awk '/^begin / {b = $2} /^end / {e = $2} END {print e - b}' targets/$t/series/$s/$b.backup
}

backup-mode() {
    typeset t=$1 s=$2 b=$3
    awk '/^mode / {print $2; exit}' targets/$t/series/$s/$b.backup
}

backup-count() {
    typeset t=$1 s=$2 b=$3
    grep -c '^+' targets/$t/series/$s/$b.eum | num2hum
}

backup-tape() {
    typeset t=$1 s=$2 b=$3
    typeset rcpt=targets/$t/series/$s/$b.t0receipt
    [[ ! -e $rcpt ]] || awk '/^T / {print $2; exit}' $rcpt
}

sec2approxdur() {
    integer s=$1
    if (( s > 3600 )); then
        print $(( (s+1800) / 3600 ))h
    elif (( s > 60 )); then
        print $(( (s+30) / 60 ))m
    else
        print ${s}s
    fi
}
make-t0p-job() {
    typeset target=$1 series=$2 mode=$3 backup=$4 site=$(zubak site) job
    typeset -a props head tail
    integer i
    props=(
        -p root=$PWD
        -p receipt=$PWD/$backup.t0receipt
        -p zubak-site=$site
        -p zubak-target=$target
        -p zubak-series=$series
        -p zubak-backup=$backup
    )
    
    # Create the t0p job
    if $dryrun; then
        print t0job -c $props >&2
        job=THE-JOB-ID-GOES-HERE
    else
        t0job -c $props | read job
        [[ -n $job ]] || exit 2
    fi
    
    # Divide into batches
    tail=( $backup.[0-9]*(NOm) )
    head=( $backup.[a-z]*(NOm) ${tail[-1]} )
    tail[-1]=()

    # Add batches to the job
    if $dryrun; then
        print t0job -a -p comment="'metadata and last data segment'" $job $head >&2
        for (( i = 1; i <= $#tail; i++ )); do
            print t0job -a -p comment=\""data segment $((i-1))"\" $job ${tail[i]} >&2
        done
        exit 0
    fi
    t0job -a -p comment='metadata and last data segment' $job $head
    for (( i = 1; i <= $#tail; i++ )); do
        t0job -a -p comment="data segment $((i-1))" $job ${tail[i]}
    done
    print $job
}

usage() {
    print "usage: $NAME ${1:-[-nvy] [-a|SUBQUEUE...]}" >&2
    exit 1
}

cancel() {
    print Cancelled >&2
    exit 0
}

fatal() {
    print ${NAME}: "$@" >&2
    exit 2
}

main "$@"
